buildscript {
    ext {
        springBootVersion = '2.4.1'
        dependencyManagementVersion = '1.0.10.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:${dependencyManagementVersion}"
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'camp.nextstep.edu'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '1.8'

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        runtimeOnly 'com.h2database:h2'

        testImplementation 'io.rest-assured:rest-assured:3.3.0'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    test {
        useJUnitPlatform()
    }
}

dependencies {
    implementation 'org.flywaydb:flyway-core'

    implementation project(':module-product')
    implementation project(':module-menugroup')
    implementation project(':module-menu')
    implementation project(':module-order')
    implementation project(':module-table')
}

project(':module-table') {
    dependencies {
        implementation project(':module-core')
    }
}

project(':module-order') {
    dependencies {
        implementation project(':module-core')
        implementation project(':module-menu')
        implementation project(':module-table')
    }
}

project(':module-product') {
    dependencies {
        implementation project(':module-core')
    }
}

project(':module-menugroup') {
    dependencies {
        implementation project(':module-core')
    }
}

project(':module-menu') {
    dependencies {
        implementation project(':module-core')
        implementation project(':module-product')
        implementation project(':module-menugroup')
    }
}
