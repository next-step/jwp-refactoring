plugins {
    id 'org.springframework.boot' version '2.4.1'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.flywaydb:flyway-core'

    testImplementation 'com.google.guava:guava:31.1-jre'
    testImplementation 'io.rest-assured:rest-assured:3.3.0'

    implementation project(':module-table')
    implementation project(':module-order')
    implementation project(':module-menu')
    implementation project(':module-product')
}

test {
    useJUnitPlatform()
    subprojects.each {
        dependsOn("${it.name}:test")
    }
}

allprojects {

    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'

    group = 'camp.nextstep.edu'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '1.8'

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        runtimeOnly 'com.h2database:h2'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }
}

subprojects {
    bootJar {
        enabled = false
    }
    jar {
        enabled = true
    }
}


project(':module-table') {
    dependencies {
        implementation project(':module-order')
    }
}
project(':module-product') {
    dependencies {
        implementation project(':module-menu')
    }
}

